apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.supportlib_version = '27.0.2'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.emerssso.livetweet"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 9
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        def key = "MISSING!"
        def secret = "MISSING!"
        try {
            Properties twitter = new Properties()
            twitter.load(project.rootProject.file('twitter.properties').newDataInputStream())
            key = twitter.consumerKey
            secret = twitter.consumerSecret
        } catch(Exception e) {
            println "twitter.properties is missing. App probably won't work."
        }

        buildConfigField "String", "TWITTER_CONSUMER_KEY", key
        buildConfigField "String", "TWITTER_CONSUMER_SECRET", secret
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warning 'InvalidPackage'
        ignore 'OnClick'
    }
    testOptions {
        unitTests {
            setReturnDefaultValues(true)
        }
        unitTests.all {
            setIgnoreFailures(true)
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportlib_version"
    implementation "com.android.support:design:$supportlib_version"
    implementation "com.android.support:cardview-v7:$supportlib_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'org.jetbrains.anko:anko-commons:0.10.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.twitter.sdk.android:twitter-core:3.1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.mikepenz:aboutlibraries:5.9.7'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'com.nhaarman:mockito-kotlin:1.4.0'
    testImplementation 'org.robolectric:robolectric:3.4.2'
}
repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
}
